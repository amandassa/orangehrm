login
click "PIM"
click "Add"
check that page contains "Add Employee"
// 1. Create new employee user
click "switch" on the right of "Create Login Details"
generate from template "%$$$$$$$", then enter into "First Name" and save it as "firstName"
generate from template "%$$$$$$$", then enter into "Last Name" and save it as "lastName"
save string with parameters "${firstName} ${lastName}" as "empName"
enter saved value "password" into "Password"
generate unique name, then enter into "Username" and save it as "empUsername"
enter saved value "password" into "Confirm Password"
click "Save"
grab value by template "####" from field "Employee Id" and save it as "empId"
check that page contains saved value "empName"
// validate by employee login
start browser "Employee Self Service" and switch
enter saved value "empUsername" into "Username"
enter saved value "password" into "Password"
click "Login"
check that page doesn't contain button "Login"
click saved value "empName"
click "Logout"
check that page contains button "Login"
close tab
switch to browser "default"
// 2. Validate form error messages: Required, Exceeded characters, Already Exists
click "Employee List"
click button "Add"
check that page contains "Add Employee"
click "switch" on the right of "Create Login Details"
click "Save"
check that page contains "Required" roughly below "Password"
check that page contains "Required" above "Employee Id" and below field "First Name"
check that page contains "Required" above button "Save" and below field "Last Name"
check that page contains "Required" above "Password" and below field "Username"
check that page contains "Passwords do not match" above "Save" and roughly below "Confirm Password"
// exceeded characters
generate by regex "[A-z]{35}", then enter into "First Name"
generate by regex "[A-z]{35}", then enter into "Last Name"
generate by regex "[A-z]{45}", then enter into "Username"
hover over the middle of the screen
check that page contains "Should not exceed 40 characters" above "Password" and below field "Username"
check that page contains "Should not exceed 30 characters" roughly below field "First Name"
check that page contains "Should not exceed 30 characters" roughly below field "Last Name"
// already exists
enter saved value "empUsername" into "Username"
check that page contains "Username already exists" roughly below "Username"
click "Cancel"
// 3. Update
click "pencil" on the right of saved value "lastName"
generate from template "%$$$$$$$", then enter into "First Name" and save it as "newFirstName"
generate from template "%$$$$$$$", then enter into "Last Name" and save it as "newLastName"
save string with parameters "${newFirstName} ${newLastName}" as "newEmpName"
click "Save"
click "PIM"
click "Employee List"
check that page doesn't contain saved value "empName" on the right of saved value "empId"
check that page contains saved value "newLastName" on the right of saved value "newFirstName"
// 4. Clean Up
click "trash" on the right of saved value "newLastName"
click "Yes, Delete"
check that page doesn't contain "loading-spinner"
check that page doesn't contain saved value "newEmpName"
